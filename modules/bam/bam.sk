include: 'bam.py'

""" Allowed Sorting Processes """
sortingTools = ('picard', 'samtools')
pipeman.updateWildcardConstraints(
  procSorted = "(sorted/({})/)?".format("|".join(sortingTools)),
  procMarkDup = "(markdup/)?",
)

bam__mappingDir   = fastq__sampleRunDir + "/{procFastqMerge}{procTrimming}mapped" + bam__alignerDir(append=True)
bam__samplePrefix = bam__mappingDir + "/{sample_name}"
bam__sample       = bam__samplePrefix + ".bam"

""" Sample Run Reads Output Targets """
bam__relDir       = "mapped" + bam__alignerDir(append=True)
bam__sampleDir    = fastq_merge__dir + "/{procTrimming}" + bam__relDir

##
# General IO (on any bam file).
bam__sortedSamplePrefix = bam__sampleDir + bam__sorterDir(append=True) + "/{sample_name}"
bam__sortedSample = bam__sortedSamplePrefix + ".bam"

""" Sample Merged Bams Output Targets """
bam__pipelineDir = "fastq/all/{procTrimming}" + bam__relDir + "/merged/{procSorted}{procMarkDup}"
bam__mergedSampleDir    = "samples/{sample_name}/" + bam__pipelineDir 
bam__mergedSamplePrefix = bam__mergedSampleDir + "{sample_name}"
bam__mergedSample       = bam__mergedSamplePrefix + ".bam"
bam__mergedSampleStats  = bam__mergedSamplePrefix + ".bamstats"

# Note: Include the aligner found in pipeline configuration file after this
#       file's variables, as aligner's rules should depend on the variables 
#       declared above.
bam__includeAlignerModule()

# -----------------
# Mark Duplicates
# -----------------
bam__sampleMarkdupPrefix = "{anyprefix}markdup/{sample_name}"
rule bam__markDuplicates:
  input: 
    bam     = "{anyprefix}{sample_name}.bam"
  output: 
    bam     = bam__sampleMarkdupPrefix + ".bam",
    bai     = bam__sampleMarkdupPrefix + ".bai",
    metrics = bam__sampleMarkdupPrefix + ".metrics"
  run:
    cmd = """
      picard MarkDuplicates                           \
      I={input.bam}                                   \
      O={output.bam}                                  \
      CREATE_INDEX=true                               \
      VALIDATION_STRINGENCY=SILENT                    \
      REMOVE_DUPLICATES=false                         \
      ASSUME_SORTED=true                              \
      METRICS_FILE={output.metrics}                    
    """
    exshell(input, output, cmd, **wildcards)

# --------------
# Sorting Bams
# --------------
rule bam__sortSamplePicard:
  input:  "{anyprefix}{sample_name}.bam"
  output: "{anyprefix}sorted/picard/{sample_name}.bam",
  run:
    cmd = """
      picard SortSam          \
        I={input}             \
        SORT_ORDER=unsorted   \
        O={output}
    """
    exshell(input, output, cmd, **wildcards)

rule bam__sortSampleSamtools:
  input:  "{anyprefix}{sample_name}.bam"
  output: "{anyprefix}sorted/samtools/{sample_name}.bam"
  run:
    cmd = """
      samtools view -Su {input}    \
       | samtools sort -o {output}  
    """
    exshell(input, output, cmd, **wildcards)

# -----------
# Bam Stats
# -----------
rule bam__mergedSampleStatsAll:
  input:
    samples_stats = lambda wildcards:
      fastq__mapStringSamples(bam__mergedSampleStats, **wildcards)
  output:
    primary = "bam__mergedSampleStatsAll.done"
  run:
    touch(output)

pipeman.toClean("bam_*.done")

rule bam__sampleStats:
  input:
    bam = "{someprefix}.bam"
  output:
    primary = "{someprefix}.bamstats"
  run:
    cmd = """
      bamtools stats       \
        -in {input.bam}    \
        > {output.primary}
    """
    exshell(input, output, cmd, **wildcards)

# ------------
# Merge BAMs
# ------------
ruleorder: bam__markDuplicates > bam__sortSamplePicard > bam__sortSampleSamtools > bam__mergeSample
rule bam__mergeSample:
  input:
    bam = lambda wildcards: 
      fastq__mapStringSamples(bam__sample, **wildcards)
  output:
    primary = bam__mergedSample,
  run:
    if len(input.bam) > 1:
      cmd = """
        picard MergeSamFiles                            \
          {merge_prefixes}                              \
          O={output.primary}                            \
          CREATE_INDEX=true                             \
          MSD=true                                      \
          TMP_DIR=$TMPDIR                               \
          VALIDATION_STRINGENCY=SILENT                  \
         > {output.primary}.log
      """
    else:
      cmd = """
        ln.rel -f {input.bam} {output.primary} 
      """
    exshell(
      input, output, cmd,
      merge_prefixes=picardMergeInputString(input.bam),
      **wildcards)

rule bam__mergeSampleAll:
  input:
    bam = lambda wildcards: fastq__mapStringSamples(
            bam__mergedSample, **wildcards)
  output:
    primary = "samples/all/" + bam__pipelineDir + "all.done" 
  run:
    touch(output)

rule bam__makeIndex:
  input:  "{someprefix}.bam"
  output: "{someprefix}.bai"
  run:
    cmd = """
      picard BuildBamIndex I={input} O={output}
    """
    exshell(input, output, cmd, **wildcards)

rule bam__hsMetrics:
  input:  "{someprefix}.bam"
  output: "{someprefix}_hsMetrics.txt"
  run:
    cmd = """
      picard CollectHsMetrics          \
        BI={baitIntervals}             \
        TI={targetIntervals}           \
        I={input}                      \
        O={output}                     \
        R={genomeFasta}                \
        VALIDATION_STRINGENCY=SILENT
    """
    exshell(
      input, output, cmd, **wildcards,
      genomeFasta = bam__genomeFasta(),
      baitIntervals = exome__baitIntervals(), 
      targetIntervals = exome__targetIntervals())

bam__sampleHsMetrics = bam__mergedSampleDir + "/merged/sorted/picard/markdup/{sample_name}_hsMetrics.txt"
rule bam__sampleHsMetricsAll:
  input:  lambda wildcards: fastq__mapStringSamples(bam__sampleHsMetrics, **wildcards)
  output: "bam__sampleHsMetricsAll.done"
  run:
    touch(output)

pipeman.toClean("bam_*.done")

