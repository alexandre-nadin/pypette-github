# -------
# Counts
# -------
# Strandedness
#   0: unstranded
#   1: forward
#   2: reverse
# -t exon :  Explicitating default value. This way only reads on exons are counted, and then summarized at gene level (by default). If '-f' is used, summarization would be at feature level (=exon by exon).*
# -C      :  do NOT count chimeric fragments in PE data, i.e. paired reads mapping on different chromosomes *
# -R BAM  :  Annotates the original bam files. We don't need this bam for now *

rule counts__featureCounts:
  input: 
    bam       = "{someprefix}/{sample_name}.bam",
    inferExp  = "{someprefix}/{sample_name}.inferExperiment",
    gtf       = lambda x: annot__ebiGtfGz()
  output:
    countsGz  =              "{someprefix}/featureCounts/{sample_name}.counts.gz",
    counts    = pypette.temp("{someprefix}/featureCounts/{sample_name}.counts"),
    sumcounts =              "{someprefix}/featureCounts/{sample_name}.counts.summary",
    bamFc     = pypette.temp("{someprefix}/featureCounts/{sample_name}.bam.featureCounts.bam"),
    bam       = pypette.temp("{someprefix}/featureCounts/{sample_name}.bam"),
    log       =              "{someprefix}/featureCounts/{sample_name}.counts.log"
  run: 
    inferExp  = rseqc__inferExperiment(input.inferExp, debug=pypette.config.debug)
    cores     = pypette.config.pipeline.modules.mapping.counter.cores
    paired    = '-p' if inferExp.isPairEnd else ''
    cmd = """
      featureCounts               \
        --tmpDir $TMPDIR          \
        -T {cores}                \
        {paired}                  \
        -C                        \
        -s {inferExp.fcStrand}    \
        -t exon                   \
        -F GTF                    \
        -a {input.gtf}            \
        -g gene_name              \
        -R BAM                    \
        -o {output.counts}        \
        {input.bam}               \
       2> {output.log}

      gzip -c {output.counts} > {output.countsGz}
  
      ln {output.bamFc} {output.bam} 
    """
    exshell(**vars())

# --------
# Merged Counts
# ---------
rule counts__mergeCounts:
  input:
    counts = pypette.input(
               f"{counts__sampleTarget}.counts.gz",
               derefKwargs=['sample_name',])
  output:
    counts = f"{counts_merged__sampleTarget}.counts.gz"
  run:
    exshell(**vars()) 
    try:
      mergedDf = counts__mergedGenesDataFrameFromCounts(input.counts)
      mergedDf.to_csv(output.counts, sep="\t", index=False, compression='gzip')
    except pd.errors.EmptyDataError as ede:
      pypette.log.warning(f"Empty Data: {ede}")

ruleorder: counts__mergeCounts > counts__featureCounts
