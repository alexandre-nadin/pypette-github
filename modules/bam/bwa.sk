include: "bwa.py"

rule bam_bwa__alignReads:
  input: 
    reads_1 = pypette.input(fastq__read, sample_read="R1"),
    reads_2 = pypette.input(fastq__read, sample_read="R2"),
    idxDir  = bam_bwa__indexDir(),
  output:
    bam     = pypette.temp(f"{bam_mapper__sampleTarget}.bam")
  run:
    readGroup = bam__sampleReadGroup(wildcards)
    bwa       = pypette.config.pipeline.modules.mapping.aligner
    reads     = bam_bwa__readsToString(input.reads_1, input.reads_2)
    idxPref   = bam_bwa__indexPrefix()
    cmd = """
      bwa mem                \
        -R "{readGroup}"     \
        -t {bwa.cores}       \
        {idxPref}            \
        {reads}              \
    | samtools view          \
        -Sb                  \
        > {output.bam}
    """
    exshell(**vars())

rule bam_bwa__index:
  input:
    fa     = genome__ucscFasta(),
  output:
    idxDir = directory(bam_bwa__indexDir()),
  run:
    idxPref = bam_bwa__indexPrefix()
    cmd = """
      mkdir -p {output.idxDir}
      bwa index      \
        -a bwtsw     \
        -p {idxPref} \
        {input.fa}
    """
    cmd, force = cmdOrLink(
      cmd,
      bam_bwa__indexDir(sharedDir=True),
      output.idxDir)
    exshell(**vars())

ruleorder: samples__runs > bam_bwa__alignReads 
