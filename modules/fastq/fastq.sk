from utils.fastq_helper import FastqFile
from utils.files import touch

include: "fastq.py"

# -------------------------------
# Wildcards Constraints
#
# Organization of the outputs follows a convention both in the naming of the 
# files as in their directories. As such, we define regex constraints on the rules' 
# wildcards. Those wildcards are defined outside of the rules, so they are 
# global to all the rules. Each constraint can then be overridden specifically 
# in the rule itself, the last declaration prevailing.
# -------------------------------------------------------------------
pipeman.updateWildcardConstraints(
  **FastqFile.regex_fields,
  preprocesses = "(/\w+/)*?",
  extension    = "\.\w+",
)
   
# --------------------- 
# I/O files format
#
# Convention: 
#   - {preprocesses} should be present both in the input and output. Can be empty.
#   - {process} is only for outputs as it is about the process defined by the
#     current rule.
# --------------------- 
fastq__dir = pipeman.workflow_dir
fastq__raw_files_io = "raw_files.txt"
fastq__mapped_samples_io     = "samples{extension}"
fastq__mapped_samples_io_dft = "samples.csv"
fastq__chunk_io = fastq__dir + "/runs/{sample_run}/{sample_chunkname}{sample_extension}"

pipeman.include("merging.sk", asWorkflow="merged")
pipeman.include("fastqc.sk")

# ---------------------
# Mapping fastq files
# ---------------------
rule fastq__mapRawFiles:
  """
  Find all the fastq files in the RUN directories specified in the 
  configuration.
  """
  input:
    configs  = config__config_files
  output: 
    primary  = fastq__raw_files_io
  run:
    runs = fastq__runsPathsProject(pipeman.project)
    cmd = """
      find {runs} -name '*.fastq.gz' > {output}
    """
    shell(cmd)

rule fastq__mapSamples:
  """   
  Maps all the illumina filenames' metadata in a file.
  """
  input: 
    file    = fastq__raw_files_io
  output: 
    primary = fastq__mapped_samples_io
  run:
    """ File Output Header """
    sample_lines = [ FastqFile.fieldNames() ]
    with open(input.file, 'r') as fastq_files:
      """ Mapped Samples Lines """
      sample_lines.extend(list(map(fastq__mapFilename, fastq_files)))

    """ Write Output """
    with open(output.primary, 'a') as fmap:
      fmap.write(
        pipeman.samples.listsToSamplesheet(
          sample_lines, 
          pipeman.sampleExtensions[wildcards.extension]))

# -----------------
# Link fastq reads
# -----------------
rule fastq__linkRead:
  """
  Links original fastq file to the project directory.
  """
  input:
    fastq_read = lambda wildcards:
      fastq__mapStringSamples("{sample_path}", **wildcards)
  output: 
    primary    = fastq__chunk_io
  shell:"""
    ln.rel {input.fastq_read} {output.primary} 
  """

rule fastq__linkReads:
  input: 
    fastq_files = lambda wildcards: 
      fastq__mapStringSamples(fastq__chunk_io, **wildcards)
  output: fastq__dir + "/fastq__linkReads.done"
  run:
    touch(output)

localrules: fastq__linkRead, fastq__linkReads

   
# ---------
# Cleaning
# ---------
pipeman.toClean("fastq_*.done")
